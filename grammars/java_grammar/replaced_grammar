program:
compilation_unit
---
identifier:
IDENTIFIER
---
qualified_identifier:
identifier
identifier OP_DOT qualified_identifier
---
qualified_identifier_list:
qualified_identifier
qualified_identifier OP_COMMA qualified_identifier_list
---
compilation_unit:
PACKAGE qualified_identifier OP_SEMI opt_import_declaration_list opt_type_declaration_list
opt_import_declaration_list opt_type_declaration_list
---
opt_import_declaration_list:
import_declaration_list

---
import_declaration_list:
import_declaration
import_declaration import_declaration_list
---
opt_type_declaration_list:
type_declaration_list

---
type_declaration_list:
type_declaration
type_declaration type_declaration_list
---
import_declaration:
IMPORT opt_static identifier_list OP_SEMI
IMPORT opt_static identifier_list OP_DOT OP_STAR OP_SEMI
---
opt_static:
STATIC

---
identifier_list:
identifier
identifier_list OP_DOT identifier
---
type_declaration:
class_or_interface_declaration
OP_SEMI
---
class_or_interface_declaration:
opt_modifier_list class_declaration
opt_modifier_list interface_declaration
---
opt_modifier_list:
modifier_list

---
modifier_list:
modifier
modifier modifier_list
---
class_declaration:
normal_class_declaration
enum_declaration
---
interface_declaration:
normal_interface_declaration 
---
opt_type_parameters:
type_parameters

---
opt_extends_type:
EXTENDS type

---
opt_implements_type_list:
IMPLEMENTS type_list

---
opt_extends_type_list:
EXTENDS type_list

---
normal_class_declaration:
CLASS identifier opt_type_parameters opt_extends_type opt_implements_type_list class_body
---
enum_declaration:
ENUM identifier opt_implements_type_list enum_body
---
normal_interface_declaration:
INTERFACE identifier opt_type_parameters opt_extends_type_list interface_body
---
opt_array_suffix_list:
array_suffix_list

---
array_suffix_list:
OP_LBRACKET OP_RBRACKET
OP_LBRACKET OP_RBRACKET array_suffix_list
---
type:
basic_type opt_array_suffix_list
reference_type opt_array_suffix_list
---
basic_type:
BYTE
SHORT
CHAR
INT
LONG
FLOAT
DOUBLE
BOOLEAN
---
reference_type:
identifier_list
identifier_list type_arguments
identifier_list type_arguments OP_DOT reference_type
---
type_arguments:
OP_LESSTHAN type_arguments_list OP_GTEATERTHAN
---
type_arguments_list:
type_argument
type_argument OP_COMMA type_arguments_list
---
type_argument:
reference_type
OP_QUESTION
OP_QUESTION EXTENDS reference_type
OP_QUESTION SUPER reference_type
---
non_wildcard_type_arguments:
OP_LESSTHAN type_list OP_GTEATERTHAN
---
type_list:
reference_type
reference_type OP_COMMA type_list
---
type_arguments_or_diamond:
OP_LESSTHAN OP_GTEATERTHAN
type_arguments
---
non_wildcard_type_arguments_or_diamond:
OP_LESSTHAN OP_GTEATERTHAN
non_wildcard_type_arguments
---
type_parameters:
OP_LESSTHAN type_parameter_list OP_GTEATERTHAN
---
type_parameter_list:
type_parameter
type_parameter OP_COMMA type_parameter_list
---
type_parameter:
identifier
identifier EXTENDS bound
---
bound:
reference_type
reference_type OP_BITAND bound
---
modifier:
PUBLIC
PROTECTED
PRIVATE
STATIC
ABSTRACT
FINAL
NATIVE
SYNCHRONIZED
TRANSIENT
VOLATILE
STRICTFP
---
class_body:
OP_LBRACE class_body_declaration_list OP_RBRACE
---
class_body_declaration_list:
class_body_declaration
class_body_declaration class_body_declaration_list
---
class_body_declaration:
OP_SEMI
opt_modifier_list member_decl
opt_static block
---
member_decl:
method_or_field_decl
VOID identifier void_method_declarator_rest
identifier constructor_declarator_rest
generic_method_or_constructor_decl
class_declaration
interface_declaration
---
method_or_field_decl:
type identifier method_or_field_rest
---
method_or_field_rest:
field_declarators_rest OP_SEMI
method_declarator_rest
---
field_declarators_rest:
variable_declarator_rest opt_comma_variable_declarator_list
---
opt_comma_variable_declarator_list:
comma_variable_declarator_list

---
comma_variable_declarator_list:
OP_COMMA variable_declarator
OP_COMMA variable_declarator comma_variable_declarator_list
---
opt_throw_qualified_identifier_list:
THROWS qualified_identifier_list

---
method_declarator_rest:
formal_parameters opt_array_suffix_list opt_throw_qualified_identifier_list block
formal_parameters opt_array_suffix_list opt_throw_qualified_identifier_list OP_SEMI
---
void_method_declarator_rest:
formal_parameters opt_throw_qualified_identifier_list block
formal_parameters opt_throw_qualified_identifier_list OP_SEMI
---
constructor_declarator_rest:
formal_parameters opt_throw_qualified_identifier_list block
---
generic_method_or_constructor_decl:
type_parameters generic_method_or_constructor_rest
---
generic_method_or_constructor_rest:
VOID identifier method_declarator_rest
type identifier method_declarator_rest
identifier constructor_declarator_rest
---
interface_body:
OP_LBRACE opt_interface_body_declaration_list OP_RBRACE
---
opt_interface_body_declaration_list:
interface_body_declaration_list

---
interface_body_declaration_list:
interface_body_declaration
interface_body_declaration interface_body_declaration_list
---
interface_body_declaration:
OP_SEMI
opt_modifier_list interface_member_decl
---
interface_member_decl:
interface_method_or_field_decl
VOID identifier void_interface_method_declarator_rest
interface_generic_method_decl
class_declaration
interface_declaration
---
interface_method_or_field_decl:
type identifier interface_method_or_field_rest
---
interface_method_or_field_rest:
constant_declarators_rest OP_SEMI
interface_method_declarator_rest
---
constant_declarators_rest:
constant_declarator_rest opt_comma_constant_declarator_list
---
opt_comma_constant_declarator_list:
comma_constant_declarator_list
---
comma_constant_declarator_list:
OP_COMMA constant_declarator
OP_COMMA constant_declarator comma_constant_declarator_list
---
constant_declarator_rest:
opt_array_suffix_list OP_EQUAL variable_initializer
---
constant_declarator:
identifier constant_declarator_rest
---
interface_method_declarator_rest:
formal_parameters opt_array_suffix_list opt_throw_qualified_identifier_list OP_SEMI
---
void_interface_method_declarator_rest:
formal_parameters opt_throw_qualified_identifier_list OP_SEMI
---
interface_generic_method_decl:
type_parameters type identifier interface_method_declarator_rest
type_parameters VOID identifier interface_method_declarator_rest
---
formal_parameters:
OP_LP OP_RP
OP_LP formal_parameter_decls OP_RP
---
formal_parameter_decls:
opt_final type formal_parameter_decls_rest
---
opt_final:
FINAL

---
formal_parameter_decls_rest:
variable_declarator_id
variable_declarator_id OP_COMMA formal_parameter_decls
ELLIPSIS variable_declarator_id
---
variable_declarator_id:
identifier opt_array_suffix_list
---
variable_declarators:
variable_declarator opt_comma_variable_declarator_list
---
variable_declarator:
identifier variable_declarator_rest
---
variable_declarator_rest:
opt_array_suffix_list
opt_array_suffix_list OP_EQUAL variable_initializer
---
variable_initializer:
array_initializer
expression
---
variable_initializer_list:
variable_initializer
variable_initializer_list OP_COMMA variable_initializer
---
array_initializer:
OP_LBRACE OP_RBRACE
OP_LBRACE variable_initializer_list OP_RBRACE
OP_LBRACE variable_initializer_list OP_COMMA OP_RBRACE
---
block:
OP_LBRACE block_statements OP_RBRACE
---
block_statements:
block_statement
block_statement block_statements
---
block_statement:
local_variable_declaration_statement
class_or_interface_declaration
statement
---
local_variable_declaration_statement:
type variable_declarators OP_SEMI
FINAL type variable_declarators OP_SEMI
---
opt_identifier:
identifier

---
statement:
block
OP_SEMI
identifier OP_COLON statement
statement_expression OP_SEMI
IF par_expression statement
IF par_expression statement ELSE statement
ASSERT expression OP_COLON expression OP_SEMI
ASSERT expression OP_SEMI
SWITCH par_expression OP_LBRACE switch_block_statement_groups OP_RBRACE
WHILE par_expression statement
DO statement WHILE par_expression OP_SEMI
FOR OP_LP for_control OP_RP statement
BREAK opt_identifier OP_SEMI
CONTINUE opt_identifier OP_SEMI
RETURN opt_expression OP_SEMI
THROW expression OP_SEMI
SYNCHRONIZED par_expression block
TRY block catches
TRY block opt_catches finally
TRY resource_specification block opt_catches
TRY resource_specification block opt_catches finally
---
statement_expression:
expression
---
opt_catches:
catches

---
catches:
catch_clause
catch_clause catches
---
catch_clause:
CATCH OP_LP opt_final catch_type identifier OP_RP block
---
catch_type:
qualified_identifier
qualified_identifier OP_BITOR catch_type
---
finally:
FINALLY block
---
resource_specification:
OP_LP resources OP_RP
OP_LP resources OP_SEMI OP_RP
---
resources:
resource
resources OP_SEMI resource
---
resource:
opt_final reference_type variable_declarator_id OP_EQUAL expression
---
switch_block_statement_groups:
switch_block_statement_group
switch_block_statement_group switch_block_statement_groups
---
switch_block_statement_group:
switch_labels block_statements
---
switch_labels:
switch_label
switch_label switch_labels
---
switch_label:
CASE expression OP_COLON
DEFAULT OP_COLON
---
opt_expression:
expression

---
opt_for_update:
for_update

---
for_control:
for_var_control
for_init OP_SEMI opt_expression OP_SEMI opt_for_update
---
for_var_control:
type variable_declarator_id for_var_control_rest
FINAL type variable_declarator_id for_var_control_rest
---
for_var_control_rest:
for_variable_declarators_rest OP_SEMI opt_expression OP_SEMI opt_for_update
OP_COLON expression
---
for_variable_declarators_rest:
opt_comma_variable_declarator_list
OP_EQUAL variable_initializer opt_comma_variable_declarator_list
---
for_init:
statement_expression
statement_expression OP_COMMA for_init
---
for_update:
statement_expression
statement_expression OP_COMMA for_update
---
expression:
expression1
expression1 assignment_operator expression1
---
assignment_operator:
OP_EQUAL
ADD_ASSIGN
SUB_ASSIGN
MUL_ASSIGN
DIV_ASSIGN
AND_ASSIGN
OR_ASSIGN
XOR_ASSIGN
MOD_ASSIGN
LEFT_ASSIGN
RIGHT_ASSIGN
GGGE_OP
---
expression1:
expression2
expression2 expression1_rest
---
expression1_rest:
OP_QUESTION expression OP_COLON expression1
---
expression2:
expression3
expression3 expression2_rest
---
expression2_rest:
infix_op_expression3_list
INSTANCEOF type
---
infix_op_expression3_list:
infix_op expression3
infix_op expression3 infix_op_expression3_list
---
infix_op:
OR_OP
AND_OP
OP_BITOR
OP_XOR
OP_BITAND
EQ_OP
NE_OP
OP_LESSTHAN
OP_GTEATERTHAN
LE_OP
GE_OP
LEFT_OP
RIGHT_OP
OP_TG
OP_ADD
OP_SUB
OP_STAR
OP_DIVIDE
OP_MOD
---
expression3:
prefix_op expression3
OP_LP type OP_RP expression3
par_expression expression3
primary opt_selector_list opt_postfix_op_list
---
opt_selector_list:
selector_list

---
opt_postfix_op_list:
postfix_op_list

---
postfix_op_list:
postfix_op
postfix_op postfix_op_list
---
selector_list:
selector
selector selector_list
---
prefix_op:
INC_OP
DEC_OP
OP_NOT
OP_BITNOT
OP_ADD
OP_SUB
---
postfix_op:
INC_OP
DEC_OP
---
primary:
literal
par_expression
THIS
THIS arguments
SUPER super_suffix
NEW creator
non_wildcard_type_arguments explicit_generic_invocation_suffix
non_wildcard_type_arguments THIS arguments
identifier_list
identifier_list identifier_suffix
basic_type opt_array_suffix_list OP_DOT CLASS
VOID OP_DOT CLASS
---
literal:
int_literal
float_literal
char_literal
string_literal
boolean_literal
null_literal
---
par_expression:
OP_LP expression OP_RP
---
expression_list:
expression
expression OP_COMMA expression_list
---
arguments:
OP_LP expression_list OP_RP
OP_LP OP_RP
---
super_suffix:
arguments
OP_DOT identifier
OP_DOT identifier arguments
---
explicit_generic_invocation_suffix:
SUPER super_suffix
identifier arguments
---
creator:
non_wildcard_type_arguments created_name class_creator_rest
created_name class_creator_rest
created_name array_creator_rest
---
created_name:
created_name_element
created_name_element OP_DOT created_name
---
created_name_element:
identifier type_arguments_or_diamond
identifier
---
class_creator_rest:
arguments
arguments class_body
---
expression_list_with_brace:
OP_LBRACKET expression OP_RBRACKET
expression_list_with_brace OP_LBRACKET expression OP_RBRACKET 
---
array_creator_rest:
expression_list_with_brace opt_array_suffix_list
array_suffix_list array_initializer
---
identifier_suffix:
OP_LBRACKET opt_array_suffix_list OP_DOT CLASS OP_RBRACKET
OP_LBRACKET expression OP_RBRACKET
arguments
OP_DOT CLASS
OP_DOT explicit_generic_invocation
OP_DOT THIS
OP_DOT SUPER arguments
OP_DOT NEW inner_creator
OP_DOT NEW non_wildcard_type_arguments inner_creator
---
explicit_generic_invocation:
non_wildcard_type_arguments explicit_generic_invocation_suffix
---
inner_creator:
identifier class_creator_rest
identifier non_wildcard_type_arguments_or_diamond class_creator_rest
---
selector:
OP_DOT identifier
OP_DOT identifier arguments
OP_DOT explicit_generic_invocation
OP_DOT THIS
OP_DOT SUPER super_suffix
OP_DOT NEW inner_creator
OP_DOT NEW non_wildcard_type_arguments inner_creator
OP_LBRACKET expression OP_RBRACKET
---
enum_body:
OP_LBRACE opt_enum_constants opt_enum_body_declarations OP_RBRACE
---
opt_enum_constants
enum_constants

---
opt_enum_body_declarations:
enum_body_declarations

---
enum_constants:
enum_constant
enum_constants OP_COMMA enum_constant
---
enum_constant:
identifier
identifier arguments
identifier class_body
identifier arguments class_body
---
enum_body_declarations:
OP_SEMI class_body_declaration_list
OP_SEMI
---
boolean_literal:
TRUE
FALSE
---
string_literal:
STRINGLITERAL
---
null_literal:
NULL
---
int_literal:
INTLITERAL
---
float_literal:
FLOATLITERAL
---
char_literal:
CHARLITERAL
---
