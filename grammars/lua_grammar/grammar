program:
chunk
---
chunk:
opt_newline_list opt_stat_list opt_last_stat
---
opt_stat_list
stat_list

---
stat_list:
stat ';' opt_newline_list stat_list
stat ';' opt_newline_list
stat newline_list stat_list
stat opt_newline_list
---
opt_last_stat:
last_stat ';' opt_newline_list
last_stat opt_newline_list

---
stat:
var_list '=' exp_list
function_call
'do' chunk 'end'
'while' exp 'do' chunk 'end'
'repeat' chunk 'until' exp
'if' exp 'then' chunk opt_elif_block_list opt_else_block 'end'
'for' identifier '=' exp ',' exp 'do' chunk 'end'
'for' identifier '=' exp ',' exp ',' exp 'do' chunk 'end'
'for' exp_list 'in' exp 'do' chunk 'end'
'function' func_name func_body
'local' 'function' identifier func_body
'local' name_list 
'local' name_list '=' exp_list
'goto' identifier
'::' identifier '::'
---
opt_elif_block_list:
elif_block_list

---
elif_block_list:
'elseif' exp 'then' chunk
'elseif' exp 'then' chunk elif_block_list
---
opt_else_block:
'else' chunk

---
last_stat:
'return' exp_list
'return'
'break'
---
func_name:
dot_name_list 
dot_name_list ':' identifier
---
dot_name_list:
identifier 
dot_name_list '.' identifier
---
var_list:
var
var ',' var_list
---
var:
identifier
pre_fix_exp '[' exp ']'
pre_fix_exp '.' identifier
---
name_list:
identifier
name_list ',' identifier
---
exp_list:
exp
exp ',' exp_list
---
exp:
'nil'
'false'
'true'
float_literal
int_literal
string_literal
'...'
function
pre_fix_exp %prec PREC_LOW
table_constructor
exp binop exp %prec PREC_HIGH
unop exp %prec PREC_HIGH
---
pre_fix_exp:
var
function_call
'(' exp ')'
---
function_call:
pre_fix_exp args 
pre_fix_exp ':' identifier args
---
args:
'(' ')'
'(' exp_list ')'
table_constructor
string_literal
---
function:
'function' func_body
---
func_body:
'(' ')' chunk 'end'
'(' par_list ')' chunk 'end'
---
par_list:
name_list
name_list ',' '...'
'...'
---
table_constructor:
'{' field_list '}'
'{' '}'
---
field_list:
field
field field_sep field_list
---
field:
'[' exp ']' '=' exp
identifier '=' exp
exp
---
field_sep:
','
';'
---
binop:
'+'
'-'
'*'
'/'
'//'
'^'
'%'
'..'
'<'
'<='
'>'
'>='
'=='
'-='
'~='
'and'
'or'
'&'
'|'
---
unop:
'-'
'not'
'#'
---
identifier:
IDENTIFIER
---
string_literal:
STRINGLITERAL
---
float_literal:
FLOATLITERAL
---
int_literal:
INTLITERAL
---
opt_newline_list
newline_list

---
newline_list:
NEWLINE newline_list
NEWLINE
---
